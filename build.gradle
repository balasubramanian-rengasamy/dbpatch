buildscript {
}

plugins {
    id 'java' // or 'groovy' Must be explicitly applied
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

ext.libraries = [
    test_libs : ['junit:junit:4.12', 'org.mockito:mockito-core:1.10.19'],
    maven_api : ['org.apache.maven:maven-plugin-api:3.3.9'],
    commons_io : ['commons-io:commons-io:2.4'],
    hsql_db : ['org.hsqldb:hsqldb:2.3.3'],

    jdbc_drivers : ['mysql:mysql-connector-java:5.1.38', 'org.postgresql:postgresql:9.4.1208']
]

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'signing'

	targetCompatibility = 1.6
    sourceCompatibility = 1.6

    group = 'org.jsoftware.dbpatch'
version = '4.2-SNAPSHOT' // do not remove that comment (used by release.sh script)

    repositories {
        mavenCentral()
        mavenLocal()
    }

    task javadocJar(type: Jar) {
      classifier = 'javadoc'
      from javadoc
    }

    task sourcesJar(type: Jar) {
      classifier = 'sources'
      from sourceSets.main.allSource
    }

    artifacts {
      archives javadocJar, sourcesJar
    }

    if (project.properties.containsKey("signing.secretKeyRingFile")) {
		signing {
		  sign configurations.archives
		}
    }

    javadoc {
        options.tags = [ "readonly", "parameter", "goal", "required", "execute" ]
    }

    if (project.properties.containsKey("ossrhUsername")) {
	    uploadArchives {
	      repositories {
        	mavenDeployer {
	          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        	  repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
	            authentication(userName: ossrhUsername, password: ossrhPassword)
        	  }
	          snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	            authentication(userName: ossrhUsername, password: ossrhPassword)
        	  }
	          pom.project {
        	    url 'https://github.com/m-szalik/dbpatch'
	            inceptionYear '2011'
        	    scm {
	              connection 'scm:git:https://github.com/m-szalik/dbpatch.git'
        	      developerConnection 'scm:git:https://github.com/m-szalik/dbpatch.git'
	              url 'https://github.com/m-szalik/dbpatch.git'
        	    }
	            licenses {
        	      license {
	                name 'The Apache License, Version 2.0'
	                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	              }
        	    }
	            developers {
        	      developer {
	                id 'm-szalik'
        	        name 'Marek Szalik'
                	email 'szalik@jsoftware.org'
	              }
        	    }
	          } // /pom.project
        	} // /mavenDeployer
	      } // /repositories
    	}
   } // if
}



